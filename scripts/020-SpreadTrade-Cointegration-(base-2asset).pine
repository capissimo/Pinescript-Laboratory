// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © capissimo

//@version=5
indicator("SpreadTrade - Cointegration (base-2asset)", "STCoint-2asset", false, timeframe="", timeframe_gaps=true) 

// Cointegration Based Pair Trading Strategy (Trading the spread)
// There are three popular styles of Pair trading:
//
// * Distance based pair trading
// * Correlation based pair trading
// * Cointegration based pair trading
//
// The Cointegration strategy is to short the outperforming instrument and go long on the underperforming instrument 
// whenever the temporary correlation weakens which means one instrument going up and another going down.
// Here, instead of two different instruments two timeframes of the same instrument are used, lower and higher. 

//-- Inputs

Asset1Symbol  = input.symbol("NQ1!",  "Asset #1", inline='a1')
Asset2Symbol  = input.symbol("ES",  "Asset #2", inline='a2')
Asset1Period  = input.int   (2,     "p:", 1, inline='a1')  
Asset2Period  = input.int   (4,     "p:", 2, inline='a2')  
AveragingFunc = input.string("WMA", "Averaging", ["WMA", "SMA"])
IsReversed    = input.bool  (false, "Reverse", inline='b')
IsMid         = input.bool  (false, "Middle line", inline='b') 

//-- Constants

MIN = -1, MAX = 1

//-- Functions

minimax(float x, int p, float min, float max) => 
    lo = ta.lowest(x, p), hi = ta.highest(x, p)
    (max - min) * (x - lo)/(hi - lo) + min

//-- Logic

X = request.security(Asset1Symbol, '', close[barstate.isrealtime?1:0])
Y = request.security(Asset2Symbol, '', close[barstate.isrealtime?1:0])

ratios = IsReversed ? math.log(Y / X) : math.log(X / Y)
ma1    = AveragingFunc=="WMA" ? ta.wma(ratios, Asset1Period) : ta.sma(ratios, Asset1Period) 
ma2    = AveragingFunc=="WMA" ? ta.wma(ratios, Asset2Period) : ta.sma(ratios, Asset2Period) 
std    = ta.stdev(ma2, Asset2Period)
zscore = ta.ema((ma1 - ma2) / std, 1)  // brings out the mean reverting nature of the ratio
//variance = minimax(ta.variance(zscore, Asset1Period), Asset1Period, -2.0, 2.0)

// Ratio is buy (1) whenever the z-score is below -1.0 because we expect z score to go back up to 0, hence ratio to increase
// Ratio is sell(-1) when the z-score is above 1.0 because we expect z score to go back down to 0, hence ratio to decrease

bool buy  = zscore < MIN //and variance > 0 and close <= close[1]
bool sell = zscore > MAX //and variance > 0 and close >= close[1]

// cointegration
//plot(wma(ratios, 100), color=orange)
plot(ratios)

plot(MAX, color=color.red)
plot(MIN, color=color.green)
hline(IsMid ? (MIN+MAX) / 2 : na, color=color.silver)
plot(zscore)
plot(sell ? zscore:na, 'Sell', color.green, 3, plot.style_circles)
plot(buy  ? zscore:na, 'Buy',  color.red,   3, plot.style_circles)
//plot(variance, color=color.black)
