// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © capissimo

//@version=5
indicator("Consolidation Filter", '', true, format=format.price, precision=2, timeframe="", timeframe_gaps=true)

//-- Description

// This script offers a fairly straightforward but effective indicator/filter of 
// indecision (consolidation) periods. A period of consolidation is characterized 
// by low volatility and uncertainty as to which way the market will go next. 
// During such periods it is better to refrain from opening positions or be 
// especially careful if a position is already open.

// This script can be used either as an overlay to the signal generator script or 
// as a subprogram in the main script. Indecision periods are highlighted with a 
// yellow background. In the settings menu you can select one of three calculation 
// methods, but all of them are based on the forecast oscillator. 

// The indicator settings depend on the asset and on the timeframe. 
// For example in BTSUSD/2H = 10/2/0.2, and in BTSUSD/15M = 10/2/0.02 

//-- Inputs

Window         = input.int   (10,    "Period", 2,  inline='cons',           group="Indecision Periods")  
IndecisionBool = input.bool  (true,  "",           inline='cons',           group="Indecision Periods")  
Method         = input.int   (2,     "Method [1..3]", 1, 3,                 group="Indecision Periods")  
Threshold      = input.float (0.02,  "Threshold [0.0..10.0]", 0, 10, 0.001, group="Indecision Periods")  

//-- Methods

method max(float data) => ta.max(data)
method ema(float data, int p)           => ta.ema(data, p) 
method mg(float data, simple int p=5) =>
    float mg = 0.0
    mg := na(mg[1]) ? data.ema(p) : mg[1] + (data - mg[1]) / (p * math.pow(data/mg[1], 4))

//-- Logic

float dataset = close[barstate.isrealtime ? 1 : 0] 

float ma  = dataset.ema(Window)  
float out = switch Method  // selects a version of forecast oscilltor 
    1 => 100 * (dataset - ma) / dataset 
    2 => 100 * (dataset - ma) / dataset.max()  
    3 => 100 * (dataset - dataset.mg(Window)) / dataset

bool within = out <= Threshold and out >= -Threshold

//-- Visuals

plot(IndecisionBool ? ma : na, "Consolidtion", within ? color.new(color.yellow, 30) : na, 20)
